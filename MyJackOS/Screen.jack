// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static int currentColor;
    static Array twoToThe;

    /** Initializes the Screen. */
    function void init() {
        let currentColor = 1;
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = ~32767;
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int address;

        let address = 16384;
        while (address < 24576) {
            do Memory.poke(address, 0);
            let address = address + 1;
        }

        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        if (b) {
            let currentColor = 1;
        } else {
            let currentColor = 0;
        }

        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int address, quotient, remainder, paint;

        if ((x < 0) | (511 < x) | (y < 0) | (255 < y)) {
            do Sys.error(7);
        }

        let quotient = x / 16;
        let address = 16384 + (y * 32) + quotient;
        let remainder = x - (quotient * 16);

        let paint = Memory.peek(address);
        if (currentColor = 1) {
            if (paint & twoToThe[remainder] = 0) {
                do Memory.poke(address, paint + twoToThe[remainder]);
            }
        } else {
            if (~(paint & twoToThe[remainder] = 0)) {
                do Memory.poke(address, paint - twoToThe[remainder]);
            }
        }

        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy, a, b, adyMinusbdx;

        if ((x1 < 0) | (511 < x1) | (x2 < 0) | (511 < x2) | (y1 < 0) | (255 < y1) | (y2 < 0) | (255 < y2)) {
            do Sys.error(8);
        }

        let dx = x2 - x1;
        let dy = y2 - y1;

        let a = 0;
        let b = 0;
        let adyMinusbdx = 0;

        if (dx = 0) {
            if (dy > 0) {
                while (~(b > dy)) {
                    do Screen.drawPixel(x1, y1 + b);
                    let b = b + 1;
                }
            } else {
                while (~(b < dy)) {
                    do Screen.drawPixel(x1, y1 + b);
                    let b = b - 1;
                }
            }

            return;
        }

        if (dy = 0) {
            if (dx > 0) {
                while (~(a > dx)) {
                    do Screen.drawPixel(x1 + a, y1);
                    let a = a + 1;
                }
            } else {
                while (~(a < dx)) {
                    do Screen.drawPixel(x1 + a, y1);
                    let a = a - 1;
                }
            }

            return;
        }

        if ((dx > 0) & (dy > 0)) {
            while (~((a > dx) | (b > dy))) {
                do Screen.drawPixel(x1 + a, y1 + b);
                if (adyMinusbdx < 0) {
                    let a = a + 1;
                    let adyMinusbdx = adyMinusbdx + dy;
                } else {
                    let b = b + 1;
                    let adyMinusbdx = adyMinusbdx - dx;
                }
            }

            return;
        }

        if ((dx > 0) & (dy < 0)) {
            while (~((a > dx) | (b < dy))) {
                do Screen.drawPixel(x1 + a, y1 + b);
                if (adyMinusbdx < 0) {
                    let b = b - 1;
                    let adyMinusbdx = adyMinusbdx + dx;
                } else {
                    let a = a + 1;
                    let adyMinusbdx = adyMinusbdx + dy;
                }
            }

            return;
        }

        if ((dx < 0) & (dy > 0)) {
            while (~((a < dx) | (b > dy))) {
                do Screen.drawPixel(x1 + a, y1 + b);
                if (adyMinusbdx < 0) {
                    let b = b + 1;
                    let adyMinusbdx = adyMinusbdx - dx;
                } else {
                    let a = a - 1;
                    let adyMinusbdx = adyMinusbdx - dy;
                }
            }

            return;
        }

        if ((dx < 0) & (dy < 0)) {
            while (~((a < dx) | (b < dy))) {
                do Screen.drawPixel(x1 + a, y1 + b);
                if (adyMinusbdx < 0) {
                    let a = a - 1;
                    let adyMinusbdx = adyMinusbdx - dy;
                } else {
                    let b = b - 1;
                    let adyMinusbdx = adyMinusbdx + dx;
                }
            }

            return;
        }
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int x, y;

        if ((x1 < 0) | (511 < x1) | (x2 < 0) | (511 < x2) | (y1 < 0) | (255 < y1) | (y2 < 0) | (255 < y2)) {
            do Sys.error(9);
        }

        if ((x2 < x1) | (y2 < y1)) {
            do Sys.error(9);
        }

        let y = y1;

        while (~(y > y2)) {
            do Screen.drawLine(x1, y, x2, y);
            let y = y + 1;
        }

        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dx, dy, dxSquare, rSqMinusDySq, MaxDy;

        if ((x < 0) | (511 < x) | (y < 0) | (255 < y)) {
            do Sys.error(12);
        }

        if (r > 181) {
            do Sys.error(13);
        }

        let dx = 0;
        if (y - r < 0) {
            let dy = -y;
        } else {
            let dy = -r;
        }
        let dxSquare = 0;
        let rSqMinusDySq = (r * r) - (dy * dy);

        while (dy < 0) {
            while (~(dxSquare > rSqMinusDySq)) {
                let dxSquare = dxSquare + dx + dx + 1;
                let dx = dx + 1;
            }
            let dxSquare = dxSquare - dx - dx + 1;
            let dx = dx - 1;

            do Screen.drawLineForCircle(x - dx, y + dy, x + dx, y + dy);
            let rSqMinusDySq = rSqMinusDySq - dy - dy - 1;
            let dy = dy + 1;
        }

        do Screen.drawLineForCircle(x - r, y, x + r, y);
        let rSqMinusDySq = rSqMinusDySq - 1;
        let dy = 1;

        if (255 - y < r) {
            let MaxDy = 255 - y;
        } else {
            let MaxDy = r;
        }

        while (~(dy > MaxDy)) {
            while (dxSquare > rSqMinusDySq) {
                let dxSquare = dxSquare - dx - dx + 1;
                let dx = dx - 1;
            }

            do Screen.drawLineForCircle(x - dx, y + dy, x + dx, y + dy);
            let rSqMinusDySq = rSqMinusDySq - dy - dy - 1;
            let dy = dy + 1;
        }

        return;
    }

    /** private function for drawCircle. */
    function void drawLineForCircle(int x1, int y1, int x2, int y2) {
        var int newX1, newX2;

        if (x1 < 0) {
            let newX1 = 0;
        } else {
            let newX1 = x1;
        }

        if (511 < x2) {
            let newX2 = 511;
        } else {
            let newX2 = x2;
        }

        do Screen.drawLine(newX1, y1, newX2, y2);

        return;
    }
}
